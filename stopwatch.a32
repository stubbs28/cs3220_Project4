; Stack begins at the very top of memory
.NAME	StkTop=8192
.NAME	UpperMin=0x100
.NAME	LowerMin=0x104
.NAME	UpperSec=0x108
.NAME	LowerSec=0x10C
; Timer limits
.NAME	Second=1000
.NAME	HalfSec=500
; Addresses for I/O
.NAME 	IOBASE= 0xF0000000
.NAME	OFSHEX= 0x00000000
.NAME	OFSLEDR=0x00000004
.NAME	OFSLEDG=0x00000008
.NAME	OFSKEY =0x00000010
.NAME	OFSSW  =0x00000014
.NAME	OFSTIMR=0x00000020
.NAME	OFSTLIM=0x00000024
.NAME   OFSKCTL=0x00000110
.NAME   OFSSCTL=0x00000114
.NAME	OFSTCTL=0x00000120

	;  Now the actual code
	.ORIG 0x40
	; SP Points to the top of the stack
	MVHI	SP,StkTop
	ADDI	SP,SP,StkTop
	; Our GP will point to the beginning of I/O addresses
	MVHI	GP,IOBASE
	; Our FP will be zero
	XOR		FP,FP,FP
Init:
	SW		FP,OFSLEDR(GP)	; Turn off LEDR
	SW		FP,OFSLEDG(GP)	; Turn off LEDG
	SW		FP,OFSHEX(GP)	; Zero Hex
	SW		FP,UpperMin(FP) 
	SW		FP,LowerMin(FP)
	SW		FP,UpperSec(FP)
	SW		FP,LowerSec(FP)
	;Set timer limit
	MVHI	T0,Second
	ADDI	T0,T0,Second
	SW		T0,OFSTLIM(GP)	; Set TLIM = 1000ms
	ADDI	S0,FP,2
	ADDI	S2,FP,1	
Reset:
	LW		T0,OFSKEY(GP)	; Get Keypress
	bne		T0,S0,Reset
Dummy0:
	LW		T0,OFSKEY(GP)	; Get Keydepress
	bne		T0,FP,Dummy0
SetSec:
	LW		T0,OFSSW(GP)	; Get Switch Value
	ANDI	S1,T0,0xF0
	SW		S1,UpperSec(FP)	; Save upper sec value
	ANDI	S1,T0,0xF
	SW		S1,LowerSec(FP)	; Save lower sec value
	CALL	DisplayHex(FP)
	LW		T0,OFSKEY(GP)	; Get Keypress
	beq		T0,S2,Init
	bne		T0,S0,SetSec
Dummy1:
	LW		T0,OFSKEY(GP)	; Get Keydepress
	bne		T0,FP,Dummy1
SetMin:
	LW		T0,OFSSW(GP)	; Get Switch Value
	ANDI	S1,T0,0xF0
	SW		S1,UpperMin(FP)	; Save upper min value
	ANDI	S1,T0,0xF
	SW		S1,LowerMin(FP)	; Save lower min value
	CALL	DisplayHex(FP)
	LW		T0,OFSKEY(GP)	; Get Keypress
	beq		T0,S2,Init
	bne		T0,S0,SetMin
Dummy2:
	LW		T0,OFSKEY(GP)	; Get Keypress
	bne		T0,FP,Dummy2	
	ADDI 	S0,FP,4			; start/stop key
Ready:
	LW		T0,OFSKEY(GP)
	beq		T0,S2,Init
	bne		T0,S0,Ready
Dummy3:
	LW		T0,OFSKEY(GP)
	bne		T0,FP,Dummy3
Runing:
	CALL	DisplayHex(FP)	; update Hex display
	CALL	CheckTick(FP)	; update countdown
	beq		RV,FP,Blink
	LW		T0,OFSKEY(GP)
	beq		T0,S2,Init
	bne		T0,S0,Runing
Dummy4:
	LW		T0,OFSKEY(GP)
	bne		T0,FP,Dummy4
	beq		T0,FP,Ready
Blink:
	;Set timer limit
	MVHI	T0,HalfSec
	ADDI	T0,T0,HalfSec
	SW		T0,OFSTLIM(GP)	; Set TLIM = 1000ms
BlinkLoop:
	LW		T0,OFSKEY(GP)	; first check if reset key is pressed
	beq		T0,S2,Init
	LW		T0,OFSTCTL(GP)	; check for tick
	beq		T0,FP,BlinkLoop	; loop if we haven't ticked
	SW		FP,OFSTCTL(GP)	; update that we've ticked
	LW		T0,OFSLEDR(GP)	; get the current LEDR
	NOT		T0,T0;			; flip current LEDR
	SW		T0,OFSLEDR(GP)	; save new LEDR
	bt		FP,FP,BlinkLoop
	
;Display the current time on the Hex
DisplayHex:
	LW		S1,UpperSec(FP)
	LW		T0,LowerSec(FP)
	ADD		S1,S1,T0
	LW		T0,UpperMin(FP)
	LW		T1,LowerMin(FP)
	ADD		T0,T0,T1
	ADDI	T1,FP,8
HexLoop:					; bitshift the miniute values
	ADD		T0,T0,T0
	ADDI	T1,T1,-1
	bne		T1,FP,HexLoop
	ADD		S1,S1,T0
	SW		S1,OFSHEX(GP)
	RET
	
CheckTick:
	LW		T0,OFSTCTL(GP)
	beq		T0,FP,EndTick	; we haven't ticked
	SW		FP,OFSTCTL(GP)	; we've ticked
	LW 		T0, LowerSec(FP)
	BEQ 	T0, FP, LowerSecIsZero
	ADDI 	T0, T0, -1
	SW 		T0, LowerSec(FP)
	BT 		EndTick
LowerSecIsZero:
	LW 		T0, UpperSec(FP)
	BEQ 	T0, FP, UpperSecIsZero
	ADDI 	T0, T0, -16
	SW 		T0, UpperSec(FP)
	ADDI 	T0, FP, 9
	SW 		T0, LowerSec(FP)
	BT 		EndTick
UpperSecIsZero:
	LW 		T0, LowerMin(FP)
	BEQ 	T0, FP, LowerMinIsZero
	ADDI 	T0, T0, -1
	SW 		T0, LowerMin(FP)
	ADDI 	T0, FP, 80 			; 80 = 0x50
	SW 		T0, UpperSec(FP)
	ADDI 	T0, FP, 9
	SW 		T0, LowerSec(FP)
	BT 		EndTick
LowerMinIsZero:
	LW 		T0, UpperMin(FP)
	BEQ 	T0, FP, Timeout
	ADDI 	T0, T0, -16
	SW 		T0, UpperMin(FP)
	ADDI 	T0, FP, 9
	SW 		T0, LowerMin(FP)
	SW 		T0, LowerSec(FP)
	ADDI 	T0, FP, 80
	SW 		T0, UpperSec(FP)
EndTick:					; return, clock is not zero
	ADDI	RV,FP,1
	RET
Timeout:					; return, clock is zero
	ADDI	RV,FP,0
	RET
	